;;;; rolls.lisp

(in-package #:alea)

(defun randint (n)
  (+ 1 (random n)))

(defclass roll ()
  ((result
    :initform nil
    :accessor result)
   (explanation
    :initform nil
    :accessor explanation)))

(defgeneric perform-roll (roll))
(defgeneric response (roll))

(defclass standard-roll (roll)
  ((dice-count
    :initarg :dice-count
    :initform (error "Must provide dice count")
    :reader dice-count)
   (die-size
    :initarg :die-size
    :initform (error "Must provide die size")
    :reader die-size)
   (modifier
    :initarg :modifier
    :initform 0
    :reader modifier)))

(defclass ore-roll (roll)
  ((dice-count
    :initarg :dice-count
    :initform (error "Must provide dice count")
    :reader dice-count)
   (expert-dice
    :initarg :expert-dice
    :initform nil
    :reader expert-dice)
   (master-die?
    :initarg :master-die?
    :initform nil
    :reader master-die?)
   (raw-rolls
    :initform nil)))

(defmethod initialize-instance :after ((roll ore-roll) &key)
           (with-slots (expert-dice master-die?) roll
             (when (and expert-dice master-die?)
               (error "You cannot specify both expert dice and a master die."))))

(defmethod print-object ((object ore-roll) stream)
  (print-unreadable-object (object stream :type t)
    (with-slots (dice-count expert-dice master-die?) object
      (let ((modifier (if master-die? "+M" (if expert-dice (format nil "毗狺撖屮疱螋溟沐┅┅ㄦ矧磲篝蝈犴滗卑立溟沐泔躅盹溟骈弪┅鏖翳箪雉蝈篚祠镡赍泗麒孱蝈篚祠ㄦ矧磲篝蝈犴⒑幄蝈篚祠┅┅ㄤ彐躅珧秕扉篝ㄦ戾è翎脲筢礤ㄩ翦愆祜镳麒殪ㄡ钿扉篝ㄥ耢ㄦ轵篝扉篝轸屙┅泔祆邈痫扉篝┅┅祜镳麒殪扉篝泔祆邈翎脲筢礤ㄦ轵篝扉篝┅┅ㄤ彐礤翳镤疱蜴矧憝蝻祆è蝻祆矧瀛蝻祆┅鏖翳箪雉ㄤ殂瀛泔躅屮疱螋溟沐磲篝弪溟蹇屮痨犷狒轱蝈篚祠蜥鳝蝻祆螬蝻祆躅戾篌蜥鳝蝻祆箦翩蜥鳝蝻祆扉篝┅换翳轶轶痱镡徕禊铒殇轱磲糸慊瘐箬铗弼弪箦磲脲盹蝈箦铙ㄤ雉轫弩ㄩ溟沐泔躅舂瘐箬蜥钿轭卑蜥鳝蝻祆螬箦翩蜥鳝蝻祆铗弼弪箦蜥鳝蝻祆螬┅躅戾篌屮痨犷狒轱戾舄è狨珥孱翦ㄩ屮疱螋溟沐ㄡ痧孱蜥鳝蝻祆屮疱螋溟沐蜥鳝蝻祆螬箫螋邃箫螋ㄣ镳箦狨珥孱翦洎＇缉ㄧ蝻躔邃ㄧ蝻躔箫螋邃┅箦翩屮痨犷狒轱狨珥孱翦洎箦翩蝈篚祠磲疸狎＇灬礅溽飑ㄦ矧磲铋澍洧戾铉翳飑ㄦ轵篝飑┅珧秕疱洎┅蝻祆┅ㄤ彐礤翳镤蝈箴镱箦è蝻祆矧瀛蝻祆┅疱蜴矧憝蝻祆蝻祆鏖翳箪雉ㄥ痨犷狒轱蝈篚祠蝻祆ㄦ矧磲铋狺): 狺" explanation result)))
